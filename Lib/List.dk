t : Base.type -> Base.type.
T : Base.type -> Type.
[a] Base.El (t a) --> T a.

nil : (a : Base.type) -> T a.
cons : (a : Base.type) -> Base.El a -> T a -> T a.

private is_eq : (a : Base.type) -> Eq.T a -> T a -> T a -> Bool.T.
[] is_eq _ _ (nil _) (nil _) --> Bool.true.
[] is_eq _ _ (nil _) (cons _ _ _) --> Bool.false.
[] is_eq _ _ (cons _ _ _) (nil _) --> Bool.false.
[a, eqA, x, t, y, u] is_eq a eqA (cons _ x t) (cons _ y u) --> Bool.and (Eq.eq a eqA x y) (is_eq a eqA t u).

def Eq (a : Base.type) (eqA : Eq.T a) : Eq.T (t a) := Eq.Cons (t a) (is_eq a eqA).

private rev_aux : (a : Base.type) -> T a -> T a -> T a.
[acc] rev_aux _ acc (nil _) --> acc.
[a, acc, x, tl] rev_aux a acc (cons _ x tl) --> rev_aux a (cons a x acc) tl.

def rev : (a : Base.type) -> T a -> T a.
[a, l] rev a l --> rev_aux a (nil a) l.

def map : (a : Base.type) -> (b : Base.type) -> (Base.El a -> Base.El b) -> T a -> T b.
[b] map _ b _ (nil _) --> nil b.
[a, b, f, x, tl] map a b f (cons _ x tl) --> cons b (f x) (map a b f tl).

private split_aux : (a : Base.type) -> Eq.T a -> Base.El a -> T (t a) -> T a -> T a -> T (t a).
[a, acc_glob, acc_loc] split_aux a _ _ acc_glob acc_loc (nil _) --> rev (t a) (cons (t a) (rev a acc_loc) acc_glob).
[a, eqA, x, acc_glob, acc_loc, hd, tl] split_aux a eqA x acc_glob acc_loc (cons _ hd tl) -->
    Bool.if_then_else (t (t a)) (Eq.eq a eqA x hd)
        (split_aux a eqA x (cons (t a) (rev a acc_loc) acc_glob) (nil a) tl)
        (split_aux a eqA x acc_glob (cons a hd acc_loc) tl).

def split : (a : Base.type) -> Eq.T a -> Base.El a -> T a -> T (t a).
[a, eqA, x, l] split a eqA x l --> split_aux a eqA x (nil (t a)) (nil a) l.

def fold_left : (a : Base.type) -> (b : Base.type) -> (Base.El b -> Base.El a -> Base.El b) -> Base.El b -> T a -> Base.El b.
[acc] fold_left _ _ _ acc (nil _) --> acc.
[a, b, f, acc, hd, tl] fold_left a b f acc (cons _ hd tl) --> fold_left a b f (f acc hd) tl.

def fold_right : (a : Base.type) -> (b : Base.type) -> (Base.El a -> Base.El b -> Base.El b) -> T a -> Base.El b -> Base.El b.
[acc] fold_right _ _ _ (nil _) acc --> acc.
[a, b, f, init, hd, tl] fold_right a b f (cons _ hd tl) init --> f hd (fold_right a b f tl init).

def foldNonEmpty : (a : Base.type) -> (Base.El a -> Base.El a -> Base.El a) -> T a -> Base.El a.
[x] foldNonEmpty _ _ (cons _ x (nil _)) --> x.
[a, f, x, y, tl] foldNonEmpty a f (cons _ x (cons _ y tl)) --> foldNonEmpty a f (cons a (f x y) tl).

def length (a : Base.type) : T a -> UnaryNat.T := fold_left a UnaryNat.t (n => _ => UnaryNat.S n) UnaryNat.0.

def prefix : (a : Base.type) -> UnaryNat.T -> T a -> T a.
[a] prefix a UnaryNat.0 _ --> nil a.
[a, n, hd, tl] prefix a (UnaryNat.S n) (cons _ hd tl) --> cons a hd (prefix a n tl).

def drop : (a : Base.type) -> UnaryNat.T -> T a -> T a.
[l] drop _ UnaryNat.0 l --> l.
[a, n, tl] drop a (UnaryNat.S n) (cons _ _ tl) --> drop a n tl.

def insert : (a : Base.type) -> Ord.T a -> Base.El a -> T a -> T a.
private insert_aux : (a : Base.type) -> Ord.T a -> Ordering.T -> Base.El a -> Base.El a -> T a -> T a.
[a, x] insert a _ x (nil _) --> cons a x (nil a).
[a, ordA, x, hd, tl] insert a ordA x (cons _ hd tl) --> insert_aux a ordA (Ord.compare a ordA x hd) x hd tl.
[a, ordA, x, hd, tl] insert_aux a ordA Ordering.LT x hd tl --> cons a hd (insert a ordA x tl).
[a, ordA, x, hd, tl] insert_aux a ordA Ordering.EQ x hd tl --> cons a hd (cons a x tl).
[a, ordA, x, hd, tl] insert_aux a ordA Ordering.GT x hd tl --> cons a x (cons a hd tl).

def sort : (a : Base.type) -> Ord.T a -> T a -> T a.
[a, ordA, l] sort a ordA l --> fold_right a (t a) (insert a ordA) l (nil a).

def insertUniq : (a : Base.type) -> Ord.T a -> Base.El a -> T a -> T a.
private insertUniq_aux : (a : Base.type) -> Ord.T a -> Ordering.T -> Base.El a -> Base.El a -> T a -> T a.
[a, x] insertUniq a _ x (nil _) --> cons a x (nil a).
[a, ordA, x, hd, tl] insertUniq a ordA x (cons _ hd tl) --> insertUniq_aux a ordA (Ord.compare a ordA x hd) x hd tl.
[a, ordA, x, hd, tl] insertUniq_aux a ordA Ordering.LT x hd tl --> cons a hd (insert a ordA x tl).
[a, ordA, x, hd, tl] insertUniq_aux a ordA Ordering.EQ x hd tl --> cons a hd tl.
[a, ordA, x, hd, tl] insertUniq_aux a ordA Ordering.GT x hd tl --> cons a x (cons a hd tl).

def sortUniq : (a : Base.type) -> Ord.T a -> T a -> T a.
[a, ordA, l] sortUniq a ordA l --> fold_right a (t a) (insertUniq a ordA) l (nil a).

private max_aux : (a : Base.type) -> Ord.T a -> Ordering.T -> Base.El a -> Base.El a -> T a -> Base.El a.
[x] max_aux _ _ Ordering.EQ x _ (nil _) --> x.
[y] max_aux _ _ Ordering.LT _ y (nil _) --> y.
[x] max_aux _ _ Ordering.GT x _ (nil _) --> x.
[a, ordA, x, hd, tl] max_aux a ordA Ordering.EQ x _ (cons _ hd tl) --> max_aux a ordA (Ord.compare a ordA x hd) x hd tl.
[a, ordA, y, hd, tl] max_aux a ordA Ordering.LT _ y (cons _ hd tl) --> max_aux a ordA (Ord.compare a ordA y hd) y hd tl.
[a, ordA, x, hd, tl] max_aux a ordA Ordering.GT x _ (cons _ hd tl) --> max_aux a ordA (Ord.compare a ordA x hd) x hd tl.

def max : (a : Base.type) -> Ord.T a -> T a -> Base.El a.
[hd] max _ _ (cons _  hd (nil _)) --> hd.
[a, ordA, x, y, tl] max a ordA (cons _  x (cons _ y tl)) --> max_aux a ordA (Ord.compare a ordA x y) x y tl.

def contains : (a : Base.type) -> Eq.T a -> Base.El a -> T a -> Bool.T.
[] contains _ _ _ (nil _) --> Bool.false.
[a, eqA, x, hd, tl] contains a eqA x (cons _ hd tl) --> Bool.if_then_else Bool.t (Eq.eq a eqA x hd) Bool.true (contains a eqA x tl).

def filter : (a : Base.type) -> (Base.El a -> Bool.T) -> T a -> T a.
[a] filter a _ (nil _) --> nil a.
[a, f, hd, tl] filter a f (cons _ hd tl) --> Bool.if_then_else (t a) (f hd) (cons a hd (filter a f tl)) (filter a f tl).

def intersect : (a : Base.type) -> Eq.T a -> T a -> T a -> T a.
[a, eqA, l1, l2] intersect a eqA l1 l2 --> filter a (x => contains a eqA x l1) l2.

def head : (a : Base.type) -> T a -> Base.El a.
[hd] head _ (cons _ hd _) --> hd.

def on : (a : Base.type) -> UnaryNat.T -> (Base.El a -> Base.El a) -> T a -> T a.
[a, f, hd, tl] on a UnaryNat.0 f (cons _ hd tl) --> cons a (f hd) tl.
[a, n, f, hd, tl] on a (UnaryNat.S n) f (cons _ hd tl) --> cons a hd (on a n f tl).

def init : (a : Base.type) -> UnaryNat.T -> Base.El a -> T a.
[a] init a UnaryNat.0 _ --> nil a.
[a, n, x] init a (UnaryNat.S n) x --> cons a x (init a n x).

def get : (a : Base.type) -> UnaryNat.T -> T a -> Base.El a.
[hd] get _ UnaryNat.0 (cons _ hd _) --> hd.
[a, n , tl] get a (UnaryNat.S n) (cons _ _ tl) --> get a n tl.

def concat : (a : Base.type) -> T a -> T a -> T a.
[l] concat _ (nil _) l --> l.
[a, hd, tl, l] concat a (cons _ hd tl) l --> cons a hd (concat a tl l).

def breakAt : (a : Base.type) -> UnaryNat.T -> T a -> Pair.T (t a) (t a).
[a, l] breakAt a UnaryNat.0 l --> Pair.pair (t a) (t a) (nil a) l.
[a, n, hd, tl] breakAt a (UnaryNat.S n) (cons _ hd tl) --> Pair.first (t a) (t a) (t a) (cons a hd) (breakAt a n tl).

def breakOnFirst : (a : Base.type) -> Eq.T a -> Base.El a -> T a -> Pair.T (t a) (t a).
[a, eqA, x, hd, tl] breakOnFirst a eqA x (cons _ hd tl) -->
Bool.if_then_else (Pair.t (t a) (t a)) (Eq.eq a eqA x hd)
    (Pair.pair (t a) (t a) (nil a) tl)
    (Pair.first (t a) (t a) (t a) (cons a hd) (breakOnFirst a eqA x tl)).

def allUniques : (a : Base.type) -> Eq.T a -> T a -> Bool.T.
[] allUniques _ _ (nil _) --> Bool.true.
[a, eqA, hd, tl] allUniques a eqA (cons _ hd tl) --> Bool.if_then_else Bool.t (contains a eqA hd tl) Bool.false (allUniques a eqA tl).

def forAll : (a : Base.type) -> (Base.El a -> Bool.T) -> T a -> Bool.T.
[a, f, l] forAll a f l --> fold_left a Bool.t (b => hd => Bool.and b (f hd)) Bool.true l.
